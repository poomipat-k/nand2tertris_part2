// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/12/Screen.jack
/**
 * A library of functions for displaying graphics on the screen.
 * The Hack physical screen consists of 512 rows (indexed 0..511, top to bottom)
 * of 256 pixels each (indexed 0..255, left to right). The top left pixel on 
 * the screen is indexed (0,0).
 */
class Screen {
    static boolean black;
    static Array screen;
    static Array powersOfTwo; // Stores 2^0, 2^1, 2^2,..., 2^(n-1)
    static Array leftToMidMask;
    static Array midToRightMask;

    /** Initializes the Screen. */
    function void init() {
        var int i, x;
        let black = true;
        let screen = 16384;
        let powersOfTwo = Array.new(16);
        let leftToMidMask = Array.new(16);
        let midToRightMask = Array.new(16);
        let powersOfTwo[0] = 1;
        let leftToMidMask[0] = 1;
        let midToRightMask[0] = -1;

        let i = 1;
        let x = 1;
        while (i < 16) {
            let x = x + x;
            let powersOfTwo[i] = x;

            let leftToMidMask[i] = leftToMidMask[i-1] + x;
            let midToRightMask[i] = midToRightMask[i-1] * 2;

            let i = i + 1;
        }
        return;
    }

    /** Erases the entire screen. */
    function void clearScreen() {
        var int i;
        let i = 0;
        while (i < 8192) {
            let screen[i] = 0;
            let i = i + 1;
        }
        return;
    }

    /** Sets the current color, to be used for all subsequent drawXXX commands.
     *  Black is represented by true, white by false. */
    function void setColor(boolean b) {
        let black = b;
        return;
    }

    /** Draws the (x,y) pixel, using the current color. */
    function void drawPixel(int x, int y) {
        var int address, xMod16, value;
        if ((x < 0) | (y < 0)) {
            do Sys.error(7);
        }
        let address = (32 * y) + (x / 16);
        let value = screen[address];
        // set the (x % 16)th bit of value to the current color
        let xMod16 = (x & 1) + (x & 2) + (x & 4) + (x & 8);
        if (black) {
            if (~(Screen.bit(value, xMod16))) {
                let value = value + powersOfTwo[xMod16];
            }
        } else {
            if (Screen.bit(value, xMod16)) {
                let value = value - powersOfTwo[xMod16];
            }
        }

        let screen[address] = value;
        return;
    }

    /** Draws a line from pixel (x1,y1) to pixel (x2,y2), using the current color. */
    function void drawLine(int x1, int y1, int x2, int y2) {
        var int dx, dy, a, b, diff;
        var int aDx, aDy, direction;

        if ((x1 < 0) | (y1 < 0) | (x2 < 0) | (y2 < 0)) {
            do Sys.error(8);
        }

        let dx = x2 - x1;
        let dy = y2 - y1;
        let a = 0; // right counter
        let b = 0; // up counter
        let diff = 0;

        if ((dx = 0) & (dy = 0)) {
            // do Screen.drawPixel(x1, y1);
            return;
        }
        // vertical
        if (dx = 0) {
            if (y2 > y1) {
                // go down
                while (y2 > y1) {
                    do Screen.drawPixel(x1, y1);
                    let y1 = y1 + 1;
                }
            } else {
                // go up
                while (y2 < y1) {
                    do Screen.drawPixel(x1, y1);
                    let y1 = y1 - 1;
                }
            }
            return;
        }

        // horizontal line
        if (dy = 0) {
            do Screen.drawHorizontalLine(x1, x2, y1);
            return;
        }


        // non straight line
        let direction = 0;
        if (dx > 0) {
            if (dy < 0) {
                // NE
                let direction = 1;
            } else {
                // SE
                let direction = 2;
            }
        } else {
            if (dy > 0) {
                // SW
                let direction = 3;
            } else {
                // NW
                let direction = 4;
            }
        }

        let aDx = Math.abs(dx);
        let aDy = Math.abs(dy);

        while (~(a > aDx) & ~(b > aDy)) {
            // direction will matter here
            if (direction = 1) {
                do Screen.drawPixel(x1 + a, y1 - b);
            }
            if (direction = 2) {
                do Screen.drawPixel(x1 + a, y1 + b);
            }
            if (direction = 3) {
                do Screen.drawPixel(x1 - a, y1 + b);
            }
            if (direction = 4) {
                do Screen.drawPixel(x1 - a, y1 - b);
            }

            if (diff < 0) {
                let a = a + 1;
                let diff = diff + aDy;
            } else {
                let b = b + 1;
                let diff = diff - aDx;
            }
        }
        return;
    }

    function void drawHorizontalLine(int x1, int x2, int y) {
        var int temp1, temp2;
        var int startBlock, endBlock, nBlocks, address;
        var int leftVal, rightVal;
        if (x1 > x2) {
            let temp1 = x2;
            let x2 = x1;
            let x1 = temp1;
        }
        let startBlock = x1 / 16;
        let endBlock = x2 / 16;
        let nBlocks = endBlock - startBlock + 1;

        if (nBlocks = 1) {
            let address = (32 * y) + startBlock;
            let leftVal = screen[address];
            let temp1 = Screen.mod16(x1);
            let temp2 = Screen.mod16(x2);
            while (~(temp1 > temp2)) {
                if (black) {
                    let leftVal = leftVal | powersOfTwo[temp1];
                } else {
                    if (leftVal & powersOfTwo[temp1] = powersOfTwo[temp1]) {
                        let leftVal = leftVal - powersOfTwo[temp1];
                    }
                }
                let temp1 = temp1 + 1;
            }
            let screen[address] = leftVal;
            return;
        }

        if (nBlocks = 2) {
            do Screen.fillLeftBlock(x1, y, startBlock);
            do Screen.fillRightBlock(x2, y, endBlock);
            return;
        }

        // nBlocks > 2
        // fill left
        do Screen.fillLeftBlock(x1, y, startBlock);
        // fill middle blocks
        let startBlock = startBlock + 1;
        while (startBlock < endBlock) {
            let address = (32 * y) + startBlock;
            if (black) {
                let screen[address] = -1;
            } else {
                let screen[address] = 0;
            }
            let startBlock = startBlock + 1;
        }
        // file right
        do Screen.fillRightBlock(x2, y, endBlock);
        return;
    }

    /** fill the left block from x1 to the end of the first block (fill mid to right) */
    function void fillLeftBlock(int x, int y, int startBlock) {
        var int address, leftVal, k;
        let address = (32 * y) + startBlock;
        let leftVal = screen[address];
        let k = Screen.mod16(x);
        if (black) {
            let leftVal = leftVal | midToRightMask[k];
        } else {
            let leftVal = leftVal & leftToMidMask[k];
        }
        let screen[address] = leftVal;
        return;
    }

    /** fill the right block from the start of right block to x2 (fill left to mid) */
    function void fillRightBlock(int x, int y, int endBlock) {
        var int address, rightVal, k;
        let address = (32 * y) + endBlock;
        let rightVal = screen[address];
        let k = Screen.mod16(x);
        if (black) {
            let rightVal = rightVal | leftToMidMask[k];
        } else {
            let rightVal = rightVal & midToRightMask[k];
        }
        let screen[address] = rightVal;
        return;
    }

    function int mod16(int x) {
        return (x & 1) + (x & 2) + (x & 4) + (x & 8);
    }

    /** Draws a filled rectangle whose top left corner is (x1, y1)
     *  and bottom right corner is (x2,y2), using the current color. */
    function void drawRectangle(int x1, int y1, int x2, int y2) {
        var int temp;
        if ((x1 < 0) | (y1 < 0) | (x2 < 0) | (y2 < 0) | (x1 = x2) | (y1 = y2)) {
            do Sys.error(9);
        }
        if (x1 > x2) {
            let temp = x1;
            let x1 = x2;
            let x2 = temp;
        }
        if (y1 > y2) {
            let temp = y1;
            let y1 = y2;
            let y2 = temp;
        }
        while (~(y1 > y2)) {
            do Screen.drawHorizontalLine(x1, x2, y1);
            let y1 = y1 + 1;
        }
        return;
    }

    /** Draws a filled circle of radius r<=181 around (x,y), using the current color. */
    function void drawCircle(int x, int y, int r) {
        var int dy, diff;
        if ((x < 0) | (y < 0)) {
            do Sys.error(12);
        }
        if ((r < 0) | (r > 181) | ((x-r) < 0) | ((x+r) > 511) | ((y-r) < 0) | ((y+r) > 255))  {
            do Sys.error(13);
        }
        let dy = -r;
        while (dy < r) {
            let diff = Math.sqrt((r*r) - (dy*dy));
            do Screen.drawLine(x - diff, y + dy, x + diff, y + dy);
            let dy = dy + 1;
        }
        return;
    }

     /** Returns true if the i-th bit of x is 1, false otherwise */
    function boolean bit(int x, int i) {
        return (x & powersOfTwo[i]) = powersOfTwo[i];
    }
}
