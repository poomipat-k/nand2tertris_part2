// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/12/String.jack
/**
 * Represents character strings. In addition for constructing and disposing
 * strings, the class features methods for getting and setting individual
 * characters of the string, for erasing the string's last character,
 * for appending a character to the string's end, and more typical
 * string-oriented operations.
 */
class String {
    field Array str;
    field int len;
    field int maxLen;

    /** constructs a new empty string with a maximum length of maxLength
     *  and initial length of 0. */
    constructor String new(int maxLength) {
        if (maxLength < 0) {
            do Sys.error(14);
        }
        if (maxLength = 0) {
            let str = Array.new(1);
            let maxLen = 0;
        } else {
            let str = Array.new(maxLength);
            let maxLen = maxLength;
        }
        let len = 0;
        return this;
    }

    /** Disposes this string. */
    method void dispose() {
        do str.dispose();
        do Memory.deAlloc(this);
        return;
    }

    /** Returns the current length of this string. */
    method int length() {
        return len;
    }

    /** Returns the character at the j-th location of this string. */
    method char charAt(int j) {
        if ((j < 0) | (~(j < len))) {
            do Sys.error(15);
        }
        return str[j];
    }

    /** Sets the character at the j-th location of this string to c. */
    method void setCharAt(int j, char c) {
        if ((j < 0) | (j > (len - 1))) {
            do Sys.error(16);
        }
        let str[j] = c;
        return;
    }

    /** Appends c to this string's end and returns this string. */
    method String appendChar(char c) {
        if (len > maxLen) {
            do Sys.error(17);
        }
        let str[len] = c;
        let len = len + 1;
        return this;
    }

    /** Erases the last character from this string. */
    method void eraseLastChar() {
        if (len = 0) {
            do Sys.error(18);
        }
        let str[len] = 0;
        let len = len - 1;
        return;
    }

    /** Returns the integer value of this string, 
     *  until a non-digit character is detected. */
    method int intValue() {
        var int val, i, d;
        var boolean neg;
        let val = 0;
        let i = 0;
        let neg = false;
        if (charAt(0) = 45) {
            let neg = true;
            let i = 1;
        }

        while (i < len) {
            let d = charAt(i);
            if ((d < 48) | (d > 57)) {
                return val;
            }
            let val = (val * 10) + (d - 48);
            let i = i + 1;
        }
        if (neg) {
            return -val;
        }
        return val;
    }

    /** Sets this string to hold a representation of the given value. */
    method void setInt(int val) {
        // clean up
        while (len > 0) {
            do eraseLastChar();
        }

        if (val < 0) {
            do appendChar(45); // - symbol
            do doSetInt(Math.abs(val));
            return;
        }
        do doSetInt(val);
        return;
    }

    method String doSetInt(int val) {
        var int lastDigit;
        var char c;
        let lastDigit = String.mod10(val);
        let c = lastDigit + 48;
        if (val < 10) {
            return appendChar(c);
        } else {
            do doSetInt(val/10);
            return appendChar(c);
        }
    }

    /** Returns the new line character. */
    function char newLine() {
        return 128;
    }

    /** Returns the backspace character. */
    function char backSpace() {
        return 129;
    }

    /** Returns the double quote (") character. */
    function char doubleQuote() {
        return 34;
    }

    function int mod10(int a) {
        return a - (10 * (a / 10));
    }
}
